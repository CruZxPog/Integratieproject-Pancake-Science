#include <Arduino.h>
#include <Wire.h>
#include <Adafruit_MLX90632.h>
#include "DFRobot_RGBLCD1602.h"
#include <WiFi.h>
#include <MQTTClient.h>
#include <ArduinoJson.h>
#include "secret.h"

/* ===== ROTARY ENCODER ===== */
#define outputA 2
#define outputB 3
int counter = 0;
int lastEncoded = 0;
unsigned long lastStepTime = 0;
const unsigned long debounceTime = 10;

/* ===== TEMPERATUURSENSOR ===== */
Adafruit_MLX90632 mlx = Adafruit_MLX90632();
const double TEMP_THRESHOLD = 37.0;

/* ===== LCD ===== */
DFRobot_RGBLCD1602 lcd(0x2D, 16, 2);

/* ===== WIFI & MQTT ===== */
WiFiClient network;
MQTTClient mqtt(256);

/* ===== TIMERS ===== */
unsigned long lastPublishTime = 0;
unsigned long lastLCDUpdateTime = 0;
const unsigned long PUBLISH_INTERVAL = 2000; // 2 sec
const unsigned long LCD_INTERVAL = 500;      // 0,5 sec

/* ===== HUIDIGE WAARDEN ===== */
int lastCounter = -999;
double lastTemp = -999.0;

/* ===== FUNCTIES ===== */
void messageHandler(String &topic, String &payload)
{
    Serial.println("Received from MQTT:");
    Serial.print("Topic: ");
    Serial.println(topic);
    Serial.print("Payload: ");
    Serial.println(payload);
}

void connectToMQTT()
{
    mqtt.begin(MQTT_BROKER, MQTT_PORT, network);
    mqtt.onMessage(messageHandler);

    Serial.print("Connecting to MQTT...");
    while (!mqtt.connect(MQTT_CLIENT_ID, MQTT_USERNAME, MQTT_PASSWORD))
    {
        Serial.print(".");
        delay(1000);
    }
    Serial.println("Connected!");

    if (mqtt.subscribe(SUBSCRIBE_TOPIC))
    {
        Serial.print("Subscribed to: ");
        Serial.println(SUBSCRIBE_TOPIC);
    }
    else
    {
        Serial.println("Failed to subscribe.");
    }
}

/* ===== SETUP ===== */
void setup()
{
    Serial.begin(115200);

    // Rotary encoder
    pinMode(outputA, INPUT_PULLUP);
    pinMode(outputB, INPUT_PULLUP);

    // I2C
    Wire.begin();
    Wire.setClock(400000);

    // LCD
    lcd.init();
    lcd.setCursor(0, 0);
    lcd.print("System start");
    lcd.setRGB(0, 255, 0);

    // MLX90632
    if (!mlx.begin())
    {
        lcd.setCursor(0, 1);
        lcd.print("Sensor fout!");
        while (1)
            ;
    }
    mlx.setMode(MLX90632_MODE_CONTINUOUS);
    mlx.setMeasurementSelect(MLX90632_MEAS_MEDICAL);
    mlx.setRefreshRate(MLX90632_REFRESH_2HZ);
    mlx.resetNewData();

    delay(1000);
    lcd.clear();

    // WiFi
    WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
    Serial.print("Connecting to WiFi");
    while (WiFi.status() != WL_CONNECTED)
    {
        delay(500);
        Serial.print(".");
    }
    Serial.println();
    Serial.print("Connected! IP: ");
    Serial.println(WiFi.localIP());

    // MQTT
    connectToMQTT();
}

/* ===== LOOP ===== */
void loop()
{
    unsigned long now = millis();

    // MQTT loop & reconnect
    mqtt.loop();
    if (!mqtt.connected() && now - lastPublishTime > 5000)
    {
        connectToMQTT();
    }

    /* ===== ROTARY ENCODER ===== */
    int MSB = digitalRead(outputA);
    int LSB = digitalRead(outputB);
    int encoded = (MSB << 1) | LSB;
    int sum = (lastEncoded << 2) | encoded;

    if (now - lastStepTime > debounceTime)
    {
        if (sum == 0b1101 || sum == 0b0100 || sum == 0b0010 || sum == 0b1011)
        {
            counter++;
            lastStepTime = now;
        }
        if (sum == 0b1110 || sum == 0b0111 || sum == 0b0001 || sum == 0b1000)
        {
            counter--;
            lastStepTime = now;
        }
    }
    lastEncoded = encoded;

    /* ===== TEMPERATUUR ===== */
    double objectTemp = lastTemp;
    if (mlx.isNewData())
    {
        objectTemp = mlx.getObjectTemperature();
        mlx.resetNewData();
    }

    /* ===== LCD UPDATE (alleen bij nieuwe waarde of interval) ===== */
    if ((objectTemp != lastTemp || counter != lastCounter) || (now - lastLCDUpdateTime > LCD_INTERVAL))
    {
        lcd.setCursor(0, 0);
        lcd.print("Pos: ");
        lcd.print(counter);
        lcd.print("    ");

        lcd.setCursor(0, 1);
        if (!isnan(objectTemp))
        {
            lcd.print("Temp: ");
            lcd.print(objectTemp, 1);
            lcd.print((char)223);
            lcd.print("C ");
            if (objectTemp >= TEMP_THRESHOLD)
                lcd.setRGB(255, 0, 0);
            else
                lcd.setRGB(0, 255, 0);
        }
        else
            lcd.print("Temp: ---.-C");

        lastLCDUpdateTime = now;
        lastCounter = counter;
        lastTemp = objectTemp;
    }

    /* ===== SERIAL ===== */
    Serial.print("Pos: ");
    Serial.print(counter);
    Serial.print(" | Temp: ");
    Serial.println(objectTemp);

    /* ===== MQTT PUBLISH (alleen bij interval) ===== */
    if (now - lastPublishTime > PUBLISH_INTERVAL)
    {
        StaticJsonDocument<128> doc;
        doc["counter"] = counter;
        doc["temp"] = objectTemp;
        char buffer[128];
        size_t n = serializeJson(doc, buffer);
        mqtt.publish(PUBLISH_TOPIC, buffer, n);
        lastPublishTime = now;
    }
}
